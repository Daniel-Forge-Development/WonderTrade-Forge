plugins {
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id("com.github.johnrengelman.shadow") version "8.1.1"
}

forgeVersion = "1.16.5"

minecraft {
    mappings channel: 'official', version: '1.16.5'
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/maven'
    }

    maven {
        url "https://maven.envyware.co.uk/releases"
    }

    ivy {
        setUrl('https://download.nodecdn.net/containers/reforged/server/release')
        metadataSources {
            artifact()
        }
        patternLayout {
            artifact('[revision]/[artifact].[ext]')
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.34'

    implementation 'pixelmon:Pixelmon-1.16.5-9.1.11-server:9.1.11'
    compileOnly 'com.github.EnvyWare.ForgePlaceholderAPI:api:2.0.3'

    shadow group: 'org.spongepowered', name: 'configurate-yaml', version: '4.0.0'
    shadow (group: 'com.envyful.api', name: 'commons', version: '6.2.1')
    shadow (group: 'com.envyful.api', name: 'forge16', version: '6.2.1') {
        transitive = false
    }
    shadow (group: 'com.envyful.api', name: 'reforged16', version: '6.2.1') {
        transitive = false
    }
}

shadowJar {
    finalizedBy('reobfJar')
    configurations = [project.configurations.shadow]
    setArchiveBaseName(rootProject.name + '-Forge.jar')

    relocate('org.spongepowered.configurate', 'com.envyful.wonder.trade.forge.shade.configurate')
    relocate('org.yaml.snakeyaml', 'com.envyful.wonder.trade.forge.shade.snakeyaml')
    relocate('io.leangen.geantyref', 'com.envyful.wonder.trade.forge.shade.geantyref')
    relocate('com.google.gson', 'com.envyful.wonder.trade.forge.shade.gson')
    relocate('com.zaxxer', 'com.envyful.wonder.trade.forge.shade.hikari')
    relocate('org.slf4j', 'com.envyful.wonder.trade.forge.shade.slf4j')
    relocate('com.envyful.api', 'com.envyful.wonder.trade.forge.shade.envy.api')
    relocate('org.bstats', 'com.envyful.wonder.trade.forge.shade.bstats')


    exclude "**/module-info.class"
}

jar.finalizedBy('shadowJar')
build.finalizedBy('versionedRelease')